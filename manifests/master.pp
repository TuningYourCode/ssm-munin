# @summary configure a munin master
#
# @see http://guide.munin-monitoring.org/en/latest/
#
# @example Basic usage
#   include munin::master
#
# @example With TLS
#   class { 'munin::master':
#     tls             => 'enabled',
#     tls_certificate => '/path/to/tls/certificate',
#     tls_private_key => '/path/to/tls/key',
#   }
#
# The munin master will install munin, and collect all exported munin
# node definitions as files into /etc/munin/munin-conf.d/.
#
# Parameters:
#
# @param node_definitions [Hash] A hash of node definitions used by
#   create_resources to make static node definitions.
#
# @param host_name [String] A host name for this munin master, matched
#   with munin::node::mastername for collecting nodes. Defaults to
#   $::fqdn
#
# @param graph_strategy [Enum['cgi','cron']] 'cgi' (default) or 'cron'
#   Controls if munin-graph graphs all services ('cron') or if
#   graphing is done by munin-cgi-graph (which must configured
#   seperatly)
#
# @param html_strategy [Enum['cgi','cron']] 'cgi' (default) or 'cron'
#   Controls if munin-html will recreate all html pages every run interval
#   ('cron') or if html pages are generated by munin-cgi-graph (which must
#   configured seperatly)
#
# @param config_root [String] the root directory of the munin master
#   configuration.  Default: /etc/munin on most platforms.
#
# @param file_group [String] The group name for configuration file
#   permissions.
#
# @param collect_nodes [Enum['enabled','disabled',
#   'mine','unclaimed']] 'enabled' (default), 'disabled', 'mine' or
#   'unclaimed'. 'enabled' makes the munin master collect all exported
#   node_definitions. 'disabled' disables it. 'mine' makes the munin
#   master collect nodes matching $munin::master::host_name, while
#   'unclaimed' makes the munin master collect nodes not tagged with a
#   host name.
#
# @param dbdir [String] Path to the munin dbdir, where munin stores
#   everything.
#
# @param htmldir [String] Path to where munin will generate HTML
#   documents and graphs, used if graph_strategy is cron.
#
# @param rundir [String] Path to directory munin uses for pid and lock
#   files.
#
# @param logdir [String] Path to directory munin uses for log files.
#
# @param package_name [String] The package name used for installing
#   the munin master.
#
# @param tls [Enum['enabled','disabled']] 'enabled' or 'disabled'
#   (default). Controls the use of TLS globally for master to node
#   communications.
#
# @param tls_certificate [String] Path to a file containing a TLS
#   certificate. No default. Required if tls is enabled.
#
# @param tls_private_key [String] Path to a file containing a TLS
#   key. No default.  Required if tls is enabled.
#
# @param tls_verify_certificate [Enum['yes','no']] 'yes' (default) or
#   'no'.
#
# @param extra_config [Array] Extra lines of config to put in
#   munin.conf.
class munin::master (
  Hash $node_definitions                                       = $munin::params::master::node_definitions,
  Enum['cgi','cron'] $graph_strategy                           = $munin::params::master::graph_strategy,
  Enum['cgi','cron'] $html_strategy                            = $munin::params::master::html_strategy,
  Stdlib::Absolutepath $config_root                            = $munin::params::master::config_root,
  Enum['enabled','disabled','mine','unclaimed'] $collect_nodes = $munin::params::master::collect_nodes,
  Optional[Stdlib::Absolutepath] $dbdir                        = $munin::params::master::dbdir,
  Optional[Stdlib::Absolutepath] $htmldir                      = $munin::params::master::htmldir,
  Optional[Stdlib::Absolutepath] $logdir                       = $munin::params::master::logdir,
  Optional[Stdlib::Absolutepath] $rundir                       = $munin::params::master::rundir,
  Enum['enabled','disabled'] $tls                              = $munin::params::master::tls,
  Optional[Stdlib::Absolutepath] $tls_certificate              = $munin::params::master::tls_certificate,
  Optional[Stdlib::Absolutepath] $tls_private_key              = $munin::params::master::tls_private_key,
  Enum['yes','no'] $tls_verify_certificate                     = $munin::params::master::tls_verify_certificate,
  String $host_name                                            = $munin::params::master::host_name,
  String $file_group                                           = $munin::params::master::file_group,
  String $package_name                                         = $munin::params::master::package_name,
  Array $extra_config                                          = $munin::params::master::extra_config,
  ) inherits munin::params::master {

  if $host_name {
    if ! is_domain_name($host_name) {
      fail('host_name should be a valid domain name')
    }
  }

  # The munin package and configuration
  package { $package_name:
    ensure => latest,
  }

  File {
    owner   => 'root',
    group   => $file_group,
    mode    => '0644',
    require => Package[$package_name],
  }

  file { "${config_root}/munin.conf":
    content => template('munin/munin.conf.erb'),
  }

  file { "${config_root}/munin-conf.d":
    ensure  => directory,
    recurse => true,
    purge   => true,
    force   => true,
  }

  if $collect_nodes != 'disabled' {
    class { '::munin::master::collect':
      collect_nodes => $collect_nodes,
      host_name     => $host_name,
    }
  }

  # Create static node definitions
  create_resources(munin::master::node_definition, $node_definitions, {})
}
